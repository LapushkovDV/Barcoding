/*************************************************************************************************\
* Наименование: Регистрация дополнительных пунктов меню                                           *
* Контур/Модуль: *                                                                                *
* Примечание:                                                                                     *
*                                                                                                 *
* Вид работы  |Номер         |Дата    |Исполнитель              |Проект                           *
* ----------------------------------------------------------------------------------------------- *
\*************************************************************************************************/

interface Shk_MenuDynamic 'Регистрация дополнительных пунктов меню' Gray;
show(20,8,80,15);

var IsClearResources:boolean;
create view
from
  X$RESOURCES
where((
  3==X$RESOURCES.XR$TYPE
));

screen ScRepParam(,,sci1Esc);
fields
  'Программа добавляет новые пункты в меню',skip,{Font={bold=true}};
  IsClearResources ('Принудительно удаляет записи с меню из X$RESOURCES',,):NoProtect;
buttons
  cmAddMenu,default,,'Добавить',,;
  cmDelMenu,,,'Удалить',,;
  cmCancel,,,'Выход',,;
<<

  .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

    [.] Удалить все меню из системной таблицы`

 <. Добавить .>     <. Удалить .>             <. ~О~тмена  .>
>>
end;

exception ExStoreMenuHowDynamics;

procedure ClearResources;
{
  if (delete X$RESOURCES <> tsok)
    message('Ошибка удаления записи в таблице X$RESOURCES!',error);
}

HandleEvent
cmInit:{
  if (not pr_CurUserAdmin) {
    message('Программа доступна только пользователям с правами администратора.',error);
    abort;
    exit;
  }
}
cmAddMenu:{
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak+vfConfirm,'Добавление новых пунктов в меню...', 0);

  if (IsClearResources) ClearResources;

  var MenuID:longint=0;
  var MenuID2:longint=0;
  _try {
    //Меню модуля "Настройка"
    MenuID:=LoadMenuEx('C_COMMON::Nastr_Admin',false,true);

    //=====================================
    //Добавление пунктов меню модуля "Настройка - администрирование - Объединение ЭМ"
    AddMenuItem(MenuID,'separator',1);
    AddSubMenuDynamic(MenuID,'C_BARCODE::Shk_Menu','МШК','','');
    //=====================================

    //=====================================
    if (not StoreMenuHowDynamics(MenuID,'C_COMMON::Nastr_Admin'))
      _raise ExStoreMenuHowDynamics;


    MenuID2:=LoadMenuEx('L_SKLAD::SklOper',false,true);
    //=====================================
    //Добавление пунктов меню модуля "Настройка - администрирование - Объединение ЭМ"
    AddMenuItem(MenuID2,'separator',1);
    AddSubMenuDynamic(MenuID2,'C_BARCODE::Shk_Menu_add_SkladOper','Штрих-кодирование','','');
    //=====================================

    //=====================================
    if (not StoreMenuHowDynamics(MenuID2,'L_SKLAD::SklOper'))
      _raise ExStoreMenuHowDynamics;

    ReinitHeaderMenu;

    Message('Дополнительные пункты меню добавлены');
  }
  _except
    on ExStoreMenuHowDynamics:
      message('Ошибка выполнения StoreMenuHowDynamics',error);
    on ExVip:
      message(ExploreException,error);
  _finally {
    if (MenuID<>0) DisposeLoadMenu(MenuID);
  }

  StopVisual;
}
cmDelMenu:{
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak+vfConfirm,'Удаление новых пунктов в меню...', 0);

  if (IsClearResources) ClearResources;

  _try {
    //Меню модуля "Настройка"
    DeleteMenuHowDynamics('C_Common::Nastr_Admin');
    DeleteMenuHowDynamics('L_SKLAD::SklOper');
    ReinitHeaderMenu;

    Message('Дополнительные пункты меню удалены')
  }
  _except
    on ExVip:
      message(ExploreException,error);

  StopVisual;
}
end;

end.

Shk_Menu Menu
{ //#3727 такой же пункт есть в УП - НПО Энергомаш - Мотивация - Настройка
   - 'Шаблоны объектов штрихкодирования', cmRunInterface('C_BARCODE::SHK_TEMPLATES'), 'Шаблоны объектов штрихкодирования. Просмотр/редактирование',,,, scMenuCtx,,,;
   - 'Просмотр и создание штрихкодов в системе', cmRunInterface('C_BARCODE::shk_barcode_view'), 'Просмотр и создание штрихкодов в системе',,,, scMenuCtx,,,;
   - 'Панель администратора', cmRunInterface('C_BARCODE::SHK_ADMIN_PANEL'), 'Панель администратора',,,, scMenuCtx,,,;
   - 'Выгрузка сальдо ячеек хранения', cmRunInterface('C_BARCODE::SHK_USB_export_saldobox'), 'Выгрузка сальдо ячеек хранения',,,, scMenuCtx,,,;
}

Shk_Menu_add_SkladOper Menu
{
   - 'Выгрузка сальдо ячеек хранения', cmRunInterface('C_BARCODE::SHK_USB_export_saldobox'), 'Выгрузка сальдо ячеек хранения',,,, scMenuCtx,,,;
}
